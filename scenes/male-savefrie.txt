John, a very close friend of yours, has been very depressed lately. You have seen him moping around the halls at school, keeping to himself. One day, while you are running an errand for a teacher on the top floor of the building, you
see a shadow swaying on the school roof.  You peer through the designs in the glass window panel, straining to see what is happening out there.
*page_break
It's John! You call out to him and discover that he is planning to jump off. He wants you to go away and make believe you didn't see him. He starts talking about wanting to be "at peace." He refers to rock stars who have died, and
how he will get a chance to be with them.
*page_break
Suddenly, while he is talking, John loses his footing and begins to fall off the roof. He is able to grab onto a flag pole that is attached to the side of the building. He looks frightened and begins to cry.
*choice mood action
  #PANIC-STRICKEN
    #GET HELP
      *goto A11
    #TRY TO HELP HIM YOURSELF
      *goto A12
  #ANXIOUS BUT TRY TO REMAIN CALM
    #GET HELP
      *goto A21
    #TRY TO HELP HIM YOURSELF
      *goto A22
  #CALM
    #GET HELP
      *goto A31
    #TRY TO HELP HIM YOURSELF
      *goto A32
*label A11
*set CA%-30
You run to look for a teacher. In the time it takes to explain the whole scenario, John falls from the school roof. He is dead before the ambulance arrives.
*finish
*label A21
*set CA%-20
*set HP%-30
You tell John to hang on for a few seconds while you get help, but as you find a teacher and tell him what has happened, John loses his grip and falls to his death.
*finish
*label A31
*if CA<50
  *goto B.T
*else
  *goto B.F
*label B.T
*set CA%-20
*set HP%-20
You are not capable of remaining calm. Your desire to appear cool under this kind of stress clouds your judgment. By the time you find someone and try to explain the situation calmly, John has already fallen to his death.
*finish
*label B.F
*set HP%-20
Your status sheet shows that you are capable of remaining calm under stress. You are able to locate a teacher quickly, and explain what is going on in as few words as possible. Unfortunately, even with your superior
decision-making and explaining, John plummets to his death.
*finish
*label A12
*set TH%-10
*set IN%+30
*set SC%+30
*set GN%+30
You act purely on impulse. You snake out onto a ledge and grab John's legs, which are dangling in the air. You save his life. Two weeks later, John feels guilty that he is a burden to you and everyone else. He tries to commit
suicide again by taking pills and alcohol together. He is saved again and taken to a hospital for observation and therapy.
*finish
*label A22
*if CN>60
  *goto C.T
*else
  *goto C.F
*label C.T
*set CN%+30
*set SC%+40
*set TH%+10
Even though this is an extremely tense situation, you are confident in your ability to save the life of your friend. You snake out onto a ledge and grab John's dangling feet. You pull him to safety inside the building. In the
following weeks, you become a school hero. The town mayor awards you with a medal honoring your courageous act.
*finish
*label C.F
Even though you have a desire to help, you lack the confidence necessary for believing that you can save John. You snake out onto a ledge and try to grab his dangling legs. You grip only one of the legs. He begins to fall. The
weight of him falling begins to pull you out of the window.


Select an action:

*choice
  #HOLD ON AND TRY TO SAVE HIM
    *goto D1
  #LET GO AND SAVE YOURSELF
    *goto D2
*label D1
*set CN%+40
*set SC%+30
Your desire to hold on to your friend could only have come from a tremendous surge of confidence (or bad judgment, but if we're going to assume, I'll give you the benefit of the doubt). You pull him into an open window, saving both
of your lives. You become a local hero and receive a medal from the mayor, honoring your courageous action.
*finish
*label D2
*set CN%+10
*set HP%-20
In horror, you watch your friend fall to a swift death. You let your lack of confidence hold you back again. You will probably feel guilty over this for some time, but the fact is that you gave a tremendously courageous effort.
You should be proud of yourself nonetheless.
*finish
*label A32
*if CA<50
  *goto E.T
*else
  *goto E.F
*label E.T
*set HP%-20
The kind of calmness you are trying to project is not part of your personality. You expend so much energy trying to appear calm that you act ineffectively. Your friend falls to his death.
*finish
*label E.F
*set CA%+10
*set CN%+30
*set SC%+40
Your status sheet shows that you do have the calmness that could carry you through this frightening experience. Your ability to keep a cool head under stress helps you make some effective on-the-spot decisions. You save the
life of your friend, and are honored for your bravery by the mayor.
*finish

