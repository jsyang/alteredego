*set instantaneous true

*temp timeunit
*temp minAge
*temp maxAge
*temp livingExpense
*temp vigCount
*temp sieCount

*if lifestage = 1
  *set minAge 0.17
  *set maxAge 3
  *set livingExpense 0
  *set vigCount 38
  *set sieCount 0
  *goto calculate
*elseif lifestage = 2
  *set minAge 4
  *set maxAge 13
  *set livingExpense 0
  *set vigCount 49
  *set sieCount 0
  *goto calculate
*elseif lifestage = 3
  *set minAge 13
  *set maxAge 19
  *set livingExpense 0.05
  *set vigCount 42
  *set sieCount 69
  *goto calculate
*elseif lifestage = 4
  *set minAge 19
  *set maxAge 35
  *set livingExpense 0.15
  *set vigCount 30
  *set sieCount 52
  *goto calculate
*elseif lifestage = 5
  *set minAge 35
  *set maxAge 45
  *set livingExpense 0.25
  *set vigCount 25
  *set sieCount 30
  *goto calculate
*elseif lifestage = 6
  *set minAge 45
  *set maxAge 65
  *set livingExpense 0.15
  *set vigCount 24
  *set sieCount 29
  *goto calculate
*else
  *set minAge 65
  *set maxAge 80
  *set livingExpense 0.1
  *set vigCount 17
  *set sieCount 26

*label calculate
*temp maxTurns
*set maxTurns round(((0.7*vigCount)+(0.2*sieCount))-0.5)
*temp phaseLength
*set phaseLength maxAge - minAge
*temp timeUnit
*set timeUnit phaseLength / maxTurns
*temp income
*set income 0
*if EM > 0
  *set income EM*timeUnit

*temp thoughtfulnessFraction
*set thoughtfulnessFraction TH/100
*temp remainingUnspentFraction
*set remainingUnspentFraction 0.5 + ((thoughtfulnessFraction/2) - livingExpense)

*comment The original game would apply the "remainingUnspentFraction" each turn
*comment ... here we apply that fraction annually instaed, by raising rUF to the
*comment timeUnit power.  That way, when we apply the new rUF after every turn,
*comment it will add up to applying the old rUF annually.  (Do the math...?)
*set remainingUnspentFraction ^timeUnit

*temp thoughtlessSpending
*set thoughtlessSpending round(MN-(MN*remainingUnspentFraction))

*temp him_opp
*if gender = "male"
  *set him_opp "her"
  *goto start
*else
  *set him_opp "him"

*label start
ALTER EGO LIFE STATUS

Occupation: ${occupation}
*line_break
Relationship Status:
*if (GF > 0) and (MS > 0)
  You're "married" to ${spouseName}, but you're cheating on ${him_opp} with ${loverName}!
  *goto kids
*elseif MS > 0
  You're married to ${spouseName}.
  *goto kids
*elseif SR > 0
  You're going steady with ${loverName}.
  *goto kids
*elseif GF > 0
  You're dating ${loverName}.
  *goto kids
*else
  UNATTACHED
*label kids
*if CL > 0
  *line_break
  *temp childcount
  *set childcount 0
  *temp childstring
  *set childstring ""
  *label childcount
  *if childcount < CL
    *if childcount > 0
      *set childstring &", "
    *set childstring &{"childname"&childcount}
    *set childcount +1
    *goto childcount
  Children: ${childstring}
*if BR > 0
  *line_break
  BAD CREDIT RATING
*label chart

*stat_chart
  percent fm Familial
  percent in Intellectual
  percent ph Physical
  percent sc Social
  percent vc Vocational
  percent ca Calmness
  percent cn Confidence
  percent ex Expressiveness
  percent gn Gentleness
  percent hp Happiness
  percent th Thoughtfulness
  percent tr Trustworthiness

Money: ${MN}
*if debt > 0
  *line_break
  Debt: ${debt}
  *goto acquisitions
*if livingExpense > 0
  *line_break
  Income per turn: ${income}
  *line_break
  Thoughtless spending per turn: ${thoughtlessSpending}

*label acquisitions

*if acquisitions = ""
  Acquisitions: (NONE)
  *goto college
*else
  Acquisitions: ${acquisitions}

*label college
*if CO > 0
  College Credits: ${CC}

*finish Next