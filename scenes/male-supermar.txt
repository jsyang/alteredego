You are running a bit late, but need to stop at the store to pick up a few things. You absolutely can't afford to waste any time. You notice that you have a few too many items to use the "Express" line (10 items or less) at the
supermarket. There is a teen-aged boy working the cash register. He probably won't notice if you try to sneak through with a few items over the limit.
*choice mood action
  #HURRIED/A BIT PANICKY
    #TAKE THE ITEMS THROUGH THE EXPRESS LANE ANYWAY
      *goto A11
    #USE THE REGULAR LANES
      *goto A12
  #CALM
    #TAKE THE ITEMS THROUGH THE EXPRESS LANE ANYWAY
      *goto A21
    *selectable_if (false) #USE THE REGULAR LANES
      *finish
*label A11
*set CA%-10
"I'm sorry, Mister, you can only take ten items through this line, you have thirteen. Would you please move to the next line?" What is your reply?


Select an action:

*choice
  #"OOPS...I'M SORRY...I'LL MOVE OVER"
    *goto B1
  #"PLEASE...DO ME A FAVOR, I'M IN A REAL HURRY AND I GOTTA GO"
    *goto B2
  #"GIMME A BREAK, KID, I GOTTA PREGNANT DOG IN THE CAR AND I REALLY HAVE TO RUN"
    *goto B3
*label B1
*set SC%+30
The clerk is shocked by your compliance. He becomes self-conscious and lets you pass through with just a warning. "O.K. but next time..."
*finish
*label B2
"I'm sorry, Sir, that sign right there says: 'Ten items or less.'" He points to the sign that is hanging six inches above your face. He refuses to let you go through.
*label E


Select an action:

*choice
  #GIVE HIM A HARD TIME
    *goto C1
  #MOVE TO THE OTHER LINE
    *goto C2
*label M1
*label K1
*label C1
*set GN%-10
He buzzes the manager, a smallish bald man with wire-rimmed glasses and a shaky voice. The manager wants no trouble so he lets you through. You have bullied your way past this rule to avoid being late.
*page_break
The checkout clerk purposely processes your order with extra care and v-e-r-y s-l-o-w-l-y. On your way out, you notice a dark brown rim on the bottom of the bag. That wise guy kid must have wet the bag...R-I-I-I-P--S-P-L-A-A-T-T!!!
*finish
*label C2
O.K.. You tried, but the kid stuck to his guns. I guess you have to admire him for that. By the way, you're now running late.
*finish
*label B3
*if GN>60
  *goto D.T
*else
  *goto D.F
*label D.T
The clerk sees that you have a sense of humor and lets you pass through the line.
*finish
*label D.F
Your Gentleness characteristic isn't quite up to the point where a remark like that would sound even mildly amusing. It comes out sounding very hostile. The clerk refuses to let you through.
*goto E
*label M2
*label K2
*label A12
There is a woman in front of you who keeps running back and forth from the check-out line to the aisles, retrieving items she "forgot." You get the distinct impression that this behavior is her "modus operandi."
*page_break
The items begin to form a mountain so large and heavy that it threatens to either halt the mechanical belt in its tracks, or require a large industrial vehicle to help cart it home. With every trip she takes away from the
counter, you lose approximately five minutes of time.


Select an action:

*choice
  #MENTION TO HER THAT SHE SHOULD DO HER SHOPPING BEFORE SHE GETS TO THE CHECK-OUT
    *goto F1
  #ASK HER IF IT WOULD BE O.K. IF YOU STEPPED AHEAD OF HER
    *goto F2
  #REMAIN CALM
    *goto F3
*label F1
"I BEG YOUR PARDON," she says with a growl.


Select an action:

*choice
  #REPEAT WHAT YOU SAID
    *goto G1
  #SAY, "FORGET IT."
    *goto G2
*label G2
*set IN%+30
*set TH%+20
A brilliant response. I mean that sincerely, not sarcastically. If you made this choice to avoid the confrontation with her, fine. I can understand that, but there is something far more complex under the surface here. Think about
it. She has been through every phase of the argument criticizing her style of shopping ten thousand times before.
*page_break
She has a million reasons to justify her behavior and ALL THE TIME IN THE WORLD. As a matter of fact, she stays awake at night hoping, dreaming and yearning to have confrontations like this so that she can recount them at
her twelve o'clock coffee gathering. You, on the other hand, are late.
*page_break
Also, since you are probably annoyed about being late, you want to avoid any other headaches that might pile on top of that. You get out of the supermarket later than ever, but you can remain calm.
*page_break
You begin to realize how ridiculous it is to get all worked up about things over which you have very little control, like being late and having to deal with people who possess unpleasent personality quirks. By the way, you forgot the eggs.
*finish
*label G1
"I simply forgot a few of my necessities. Do you have a PROBLEM with this?" she says. You can say:


Select an action:

*choice
  #"I ONLY HAVE A PROBLEM WITH THIS WHEN I AM ALREADY LATE AND IT IS MAKING ME EVEN LATER"
    *goto H1
  #"IF I HAD A PROBLEM WITH IT I WOULD REMOVE IT, LIKE ALL MY OTHER PROBLEMS"
    *goto H2
  #"I HAVE NO PROBLEM WITH ONE OR TWO ITEMS, BUT YOUR FORGETFULNESS EXTENDS A BIT BEYOND WHAT I CAN BE COMFORTABLE WITH"
    *goto H3
*label H1
*set GN%-10
"So, it's MY problem that YOU'RE late, Mr. Bigshot? That's too bad, isn't it?"
This conversation can go on forever, but why get aggravated?


Select an action:

*choice
  #TUNE HER OUT
    *goto I1
  #CONTINUE ARGUING WITH HER
    *goto I2
*label I1
*set IN%+20
Good idea. You are later than ever and now frustrated besides. Fortunately, you will probably never run into her again, and if you do, you'll probably avoid her.
*finish
*label I2
*set PH%-10
*set CA%-10
Very stubborn of you. She has this argument every day of her life and will not admit that her style of shopping is an inconvenience to others. Your frustration level increases.
*finish
*label H2
*set SC%-20
"Are you THREATENING me, young man? ME?? AN OLDER, HELPLESS WOMAN?? MANAGER! MANAGER! THIS MAN IS THREATENING ME WITH PHYSICAL VIOLENCE!" The manager comes and you are asked to leave the store before any trouble starts. You try to
explain yourself, but it seems that eight other shoppers who look like clones of this woman have come to her defense.
*page_break
One mentions that you bear a strong resemblance to a poster of a criminal she saw in the post office. You can't believe this is happening, but it is. I guess you were a bit too aggressive.
*finish
*label H3
*label F2
*set IN%+20
This is a very polite and assertive response. Unfortunately, the woman makes believe she doesn't even hear you. She believes she is perfectly justified in "forgetting" a few items. After all, she is getting old. She mentions that a
fellow like you shouldn't have to do your own shopping.
*page_break
She has this daughter who is pretty and available (recently divorced from a real jerk, you know), blah, blah, blah... She continues along these lines for what seems to be an eternity, until finally you escape. By the way, you
forgot the eggs.
*finish
*label A21
You notice the cashier counting the number of items in your cart. He is about to say something.


Select an action:

*choice
  #REMAIN QUIET
    *goto J1
  #ENGAGE HIM IN SOME IDLE CONVERSATION
    *goto J2
*label J1
Mistake--he sees that you are a few items over and refers you to the sign that says: "EXPRESS LANE: 10 ITEMS OR LESS"


Select an action:

*choice
  #GIVE HIM A HARD TIME
    *goto K1
  #USE THE REGULAR LANES
    *goto K2
*label J2
*if SC>70
  *goto L.T
*else
  *goto L.F
*label L.T
*set CN%+30
Very smooth -- you have great social skills, so you can employ this tactic. He lets you slide through and you are back on time.
*finish
*label L.F
*set CN%-20
I'm sorry. You don't have the social skills to employ this kind of tactic. You are told to move to the regular lanes.


Select an action:

*choice
  #GIVE HIM A HARD TIME
    *goto M1
  #USE THE REGULAR LANES
    *goto M2
*label F3
*set CA%+20
Your calmness and patience are amazing. You leave the store an hour off schedule.
*finish

