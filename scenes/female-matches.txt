While trying to think of what to do one day, you notice that you could use a nice haircut. Mommy's favorite pair of shiny scissors is lying out on the table. You are sure you could do the job yourself.
*choice mood action
  #CONSCIENTIOUS
    #CHANGE YOUR MIND
      *goto A11
    *selectable_if (false) #CLIP A LITTLE PIECE OF YOUR HAIR OFF
      *finish
    *selectable_if (false) #BRING THE SCISSORS INTO YOUR ROOM AND START GIVING YOURSELF A HAIRCUT
      *finish
  #CURIOUS
    *selectable_if (false) #CHANGE YOUR MIND
      *finish
    #CLIP A LITTLE PIECE OF YOUR HAIR OFF
      *goto A22
    #BRING THE SCISSORS INTO YOUR ROOM AND START GIVING YOURSELF A HAIRCUT
      *goto A23
*label A11
*set TR%+60
You may have just prevented a situation which could have ended with some of your friends nicknaming you "Mrs. T."
*finish
*label A22
*set TR%-10
This isn't as easy as it looks. You pull up a long strand of hair and listen to the sound of the scissors going "Shhhhhhhh," then closing with a loud "CLICK!" Suddenly, your mother is aroused by the lack of commotion, usually
accompanying sessions of solitary play. "WHAT ARE YOU DOING, YOUNG LADY?" Your career as a hairstylist has suffered an early blow.
*finish
*label A23
Now that you have the scissors in the room, you can:


Select an action:

*choice
  #START CUTTING
    *goto B1
  #HIDE THE SCISSORS
    *goto B2
*label B1
*set TH%-30
You begin by taking small wisps of hair off the top and front.


Select an action:

*choice
  #KEEP CUTTING
    *goto C1
  #HIDE THE SCISSORS
    *goto C2
*label C1
*set TH%-30
CLIP! CLIP! CLIP! This is easy. The sides are a little uneven.


Select an action:

*choice
  #KEEP CUTTING
    *goto D1
  #HIDE THE SCISSORS
    *goto D2
*label D1
*set TH%-20
You are beginning to worry that you may have cut a piece or two a bit short. Perhaps some shampoo would fluff your hair up and make the uneven spots a bit less obvious.


Select an action:

*choice
  #SHAMPOO YOUR (REMAINING) HAIR
    *goto E1
  #HIDE THE SCISSORS
    *goto E2
*label E1
*set TH%-20
You run into the bathroom and quickly close the door behind you. Placing your head under the faucet, you let a thick stream of Mommy's shampoo slide onto your head.

*temp randomZ
*rand randomZ 1 2
*if randomZ=1
  *goto Z1
*else
  *goto Z2
*label Z1
In your excitement, you neglect to control the water temperature carefully. Your scalp begins to burn:


Select an action:

*choice
  #DO NOT SAY ANYTHING
    *goto F1
  #GET MOM
    *goto F2
*label F1
*set CA%-30
It's too late. You have already burned your head rather badly.
*page_break
*temp randomG
*rand randomG 1 2
*if randomG=1
  *goto G1
*else
  *goto G2
*label G1
*set PH%-30
Your mom finds out, of course, but she is so worried about the burn, that she doesn't have time to yell at you for what you did. You are taken to the hospital emergency room and treated for your injuries. You are lucky not to have been
hurt too seriously. Your hair is a mess!!
*finish
*label G2
*set PH%-20
Your mom knocks on the bathroom door and can tell that something is wrong. Your head is hurting so much, she has to break the lock and go in after you. You have burned your scalp, but fortunately, not seriously.
*finish
*label F2
*set CA%-10
*set TH%-30
You were smart to ask Mom for help. You could have scalded your head very badly. Fortunately, you have avoided burning yourself. Mom asks you where you got the scissors.


Select an action:

*choice
  #KEEP QUIET
    *goto H1
  #CONFESS THAT YOU TOOK THEM FROM THE TABLE
    *goto H2
*label H1
*set TR%-10
Your silence is taken as an admission of your guilt. Do you feel guilty about what you have done?


Select an action:

*choice
  #YES
    *goto I1
  #NO
    *goto I2
*label I1
*set TR%+10
At least you have a conscience.
*finish
*label I2
*set TR%-50
Your desire to "create" a new hairstyle was admirable; however, your lack of conscience is a bad sign. You must admit your lack of judgment could have gotten you into more trouble than you bargained for. Later, you may not be so lucky.
*finish
*label H2
*set HP%-30
They say, "Confession is good for the soul." It sure doesn't seem that way now. Your mother is very upset. She feels what has happened is partly her fault for leaving the scissors out. This only makes you feel worse.
*finish
*label Z2
When you are finished, your hair looks like the latest "punk rock" creation.


Select an action:

*choice
  #HIDE THE SCISSORS AND TRY TO AVOID BEING SEEN UNTIL IT GROWS BACK
    *goto J1
  #TELL YOUR MOM WHAT YOU DID
    *goto J2
*label J1
*label B2
*label D2
*label E2
*label C2
Eventually, you are found out and confronted. Your mom asks, "WHAT DID YOU DO WITH THESE SCISSORS?"


Select an action:

*choice
  #CONFESS
    *goto K1
  #SAY, "I DON'T KNOW."
    *goto K2
*label K1
*set TR%+40
*label TRUTH
Your mom admires your truthfulness. She isn't worried about your hair as much as she is worried that you could have hurt yourself badly with the scissors.
*finish
*label K2
*set TR%-10
*set HP%-20
Nothing like adding insult to injury. Your mom knows what you did. (She was a kid once too.) She is upset mostly because you could have hurt yourself with the scissors. You are grounded for two weeks.
*finish
*label J2
*set TR%+20
*goto TRUTH

